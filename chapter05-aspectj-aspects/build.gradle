plugins {
    id 'java'
    id 'io.freefair.aspectj.base' version '5.1.0'
    id 'io.freefair.aspectj' version '5.1.0'
}

repositories {
    mavenCentral()
}


/*Task that copies all the dependencies under build/libs */
task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs'
}

dependencies {
    implementation 'org.springframework:spring-context:5.2.7.RELEASE'
    implementation 'org.aspectj:aspectjrt:1.9.5', 'org.aspectj:aspectjweaver:1.9.5'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2', 'org.springframework:spring-test:5.2.7.RELEASE'
}

compileAspectj {
    source("src/main/java")
    setDestinationDir("build/classes/java/main" as File)
}

jar {
    manifest {
        attributes(
            "Created-By": "Iuliana Cosmina",
            "Specification-Title": "Pro Spring 5",
            "Main-Class": "com.apress.prospring5.ch5.AspectJDemo",
            "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

/*tasks.withType(Test) {
    logging.captureStandardOutput LogLevel.QUIET
    logging.captureStandardError LogLevel.QUIET
    testLogging.showStandardStreams = true
}*/

/*最后将依赖项赋值到 lib 目录*/
tasks.build.finalizedBy(copyDependencies)

